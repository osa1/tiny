# Maintainer: Jonathan Kirszling <jonathan.kirszling at runbox dot com>
# Maintainer: Ralph Torres <mail at ralphptorr dot es>
# Contributor: Nick Econopouly <wry at mm dot st>

pkgname=tiny-irc-client-git
pkgver=0.11.0.r18.e125c77
pkgrel=1
pkgdesc='A terminal IRC client written in Rust'
arch=(x86_64)
url=https://github.com/osa1/tiny
license=(MIT)

depends=(dbus)
makedepends=(git cargo)
provides=(${pkgname%-git})
conflicts=(${pkgname%-git})
source=(git+$url)
sha512sums=(SKIP)

_pkgname=${pkgname%-irc-client-git}

pkgver() {
    cd "$srcdir"/$_pkgname
    git describe --tags --long --abbrev=7 |\
        sed 's/\([^-]*-\)g/r\1/;s/-/./g;s/^v//'
}

prepare() {
    cd "$srcdir"/$_pkgname
    cargo update
    cargo fetch --locked --target $CARCH-unknown-linux-gnu
}

build() {
    cd "$srcdir"/$_pkgname
    cargo build --frozen --release --features desktop-notifications
}

check() {
    cd "$srcdir"/$_pkgname
    cargo test --frozen --workspace --features desktop-notifications
}

package() {
    cd "$srcdir"/$_pkgname
    install -Dm755 -t "$pkgdir"/usr/bin target/release/$_pkgname
    install -Dm644 -t "$pkgdir"/usr/share/licenses/$_pkgname LICENSE
    install -Dm644 -t "$pkgdir"/usr/share/doc/$_pkgname \
        ARCHITECTURE.md CHANGELOG.md README.md
    install -Dm644 -t "$pkgdir"/usr/share/$_pkgname crates/$_pkgname/config.yml
}
